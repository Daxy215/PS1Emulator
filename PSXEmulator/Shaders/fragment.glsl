#version 420 core

in vec3 color;

//uniform ivec2 drawingAreaMin;
//uniform ivec2 drawingAreaMax;
flat in ivec2 drawingAreaMinIn;
flat in ivec2 drawingAreaMaxIn;
in vec2 VRAMPos;

/*
 * Because I'm too lazy,
 * I decided to add clut position,
 * to this, so, x, y are the uvs,
 * and z and w are the clut positions.
 * 
 * Because I'm even more lazy,
 * I split the data into a single float,
 * so:
 * UVs.z, has clutX AND pageX
 * UVs.w, has clutY AND pageY
 */
in vec4 UVs;

/**
 * 0 = isSemiTransparent
 * 1 = blendTexture
 * 2 = useTextures ( 0 = No texture, 1 = Only texture, 2 = Texture + Color)
 */
flat in int attr;

out vec4 fragColor;

layout(binding = 0) uniform sampler2D texture_sample4;
layout(binding = 1) uniform sampler2D sceneTex;

//uniform int texture_depth;
uniform vec4 textureWindow;
uniform int semiTransparencyMode;

const int IS_SEMITRANSPARENT_MASK = 0x1;
const int BLEND_TEXTURE_MASK = 0x2;
const int TEXTURE_MODE_MASK = 0x1C;
const int TEXTURE_MODE_SHIFT = 2;
const int TEXTURE_DEPTH_SHIFT = 5;
const int TEXTURE_DEPTH_MASK = 0x3;

vec2 calculateTexel() {
    uvec2 texel = uvec2(uint(UVs.x) % 256u, uint(UVs.y) % 256u);

    uvec2 mask = uvec2(textureWindow.x, textureWindow.y);
    uvec2 offset = uvec2(textureWindow.z, textureWindow.w);

    texel.x = (texel.x & ~(mask.x * 8u)) | ((offset.x & mask.x) * 8u);
    texel.y = (texel.y & ~(mask.y * 8u)) | ((offset.y & mask.y) * 8u);

    return vec2(texel);
}

uint internalToPsxColor(vec4 c) {
    uint a = uint(floor(c.a + 0.5));
    uint r = uint(floor(c.r * 31.0 + 0.5));
    uint g = uint(floor(c.g * 31.0 + 0.5));
    uint b = uint(floor(c.b * 31.0 + 0.5));

    return (a << 15) | (b << 10) | (g << 5) | r;
}

vec4 read(int x, int y) {
    y = 512 - 1 - y;
    
    return texelFetch(texture_sample4, ivec2(x, y), 0);
}

vec4 clut4bit(vec2 coords, ivec2 clut, ivec2 page) {
    int texX = int(coords.x / 4.0) + page.x;
    int texY = int(coords.y)       + page.y;

    uint index = internalToPsxColor(read(texX, texY));
    uint which = (index >> ((uint(coords.x) & 3u) * 4u)) & 0xfu;

    return read(clut.x + int(which), clut.y);
}

vec4 clut8bit(vec2 coords, ivec2 clut, ivec2 page) {
    int texX =  int(coords.x / 2.0) + page.x;
    int texY =  int(coords.y)       + page.y;

    uint index = internalToPsxColor(read(texX, texY));
    uint which = (index >> ((uint(coords.x) & 1u) * 8u)) & 0xffu;

    return read(int(which) + clut.x, clut.y);
}

vec4 clut16bit(vec2 coords, ivec2 page) {
    return read(int(coords.x) + page.x, int(coords.y) + page.y);
}

vec4 sample_texel(int textureDepth) {
    /*
     * Extract clut and page
     */
    float clutX = int(UVs.z) >> 16;
    float clutY = int(UVs.w) >> 16;

    float pageX = int(UVs.z) & 0xFFFF;
    float pageY = int(UVs.w) & 0xFFFF;

    vec2 texel = calculateTexel();

    /**
     * T4Bit = 0,
     * T8Bit = 1,
     * T15Bit = 2,
     */
    if (textureDepth == 0) {
        return clut4bit(texel, ivec2(clutX, clutY), ivec2(pageX, pageY));
    } else if(textureDepth == 1) {
        return clut8bit(texel, ivec2(clutX, clutY), ivec2(pageX, pageY));
    } else if(textureDepth == 2) {
        return clut16bit(texel, ivec2(pageX, pageY));
    }

    return vec4(1, 0, 0, 1);
}

void main() {
    //int y = int(VRAMPos.y);
    //fragColor = read(int(VRAMPos.x), y);
    
    //return;
    
    // Crop
    if (any(lessThan(VRAMPos, drawingAreaMinIn)) ||
        any(greaterThanEqual(VRAMPos, drawingAreaMaxIn))) {
        discard;
    }
    
    /**
     * 0 = isSemiTransparent
     * 1 = blendTexture
     * 2 = textureMode ( 0 = No texture, 1 = Only texture, 2 = Texture + Color)
     */
    int isSemiTransparent = int(attr) & IS_SEMITRANSPARENT_MASK;
    int blendTexture = (int(attr) & BLEND_TEXTURE_MASK) >> 1;
    int textureMode = (int(attr) & TEXTURE_MODE_MASK) >> TEXTURE_MODE_SHIFT;
    
    // Apply transparency
    vec4 outColor, samp, F;
    
    // textureMode ( 0 = No texture, 1 = Only texture, 2 = Texture + Color)
    if(textureMode == 1 || textureMode == 2) {
        int textureDepth = (attr >> TEXTURE_DEPTH_SHIFT) & TEXTURE_DEPTH_MASK;
        samp = sample_texel(textureDepth);
        
        // If black just ignore
        if(internalToPsxColor(samp) == 0u) {
            discard;
        }
    }
    
    // textureMode ( 0 = No texture, 1 = Only texture, 2 = Texture + Color)
    if (textureMode == 0) {
        if (blendTexture == 1) {
            // Texel = 128 (0x80) because TME=0
            vec3 col255 = color * 255.0;
            vec3 tex255 = vec3(128.0);
            
            vec3 blended255 = floor((tex255 * col255) / 128.0);
            F.rgb = blended255 / 255.0;
            F.a = 1.0;
        } else {
            F = vec4(color, 1.0);
        }
    } else {
        // Textured primitives
        if (textureMode == 1) {
            F = samp;
        } else { // textureMode == 2
            F = samp * vec4(color, 1.0);
        }
        
        if (blendTexture == 1) {
            // finalChannel.rgb = (texel.rgb * vertexColour.rgb) / vec3(128.0)
            vec3 tex5 = samp.rgb * 31.0;
            vec3 tex255 = tex5 * (255.0 / 31.0);
            vec3 col255 = color * 255.0;
            
            vec3 blended255 = floor((tex255 * col255) / 128.0);
            
            F.rgb = blended255 / 255.0;
            F.a   = samp.a;
        }
    }
    
    /*
     * B=Back  (the old pixel read from the image in the frame buffer)
     * F=Front (the new halftransparent pixel)
     * 0.5 x B + 0.5 x F    ;aka B/2+F/2
     * 1.0 x B + 1.0 x F    ;aka B+F
     * 1.0 x B - 1.0 x F    ;aka B-F
     * 1.0 x B +0.25 x F    ;aka B+F/4
     */
    if(isSemiTransparent == 1) {
        float gamma = 2.0;
        float invGamma = 1.0 / gamma;
        
        //int y = int(VRAMPos.y);
        int y = drawingAreaMaxIn.y - int(VRAMPos.y);
        //vec4 B = read(int(VRAMPos.x), y);
        //vec4 B = texelFetch(sceneTex, ivec2(int(VRAMPos.x), y), 0);
        vec4 B = vec4(0, 0, 0, 1);
        
        vec3 Blinear = pow(B.rgb, vec3(gamma));
        vec3 Flinear = pow(F.rgb, vec3(gamma));
        
        vec3 Bi = floor(Blinear * 31.0 + 0.5);
        vec3 Fi = floor(Flinear * 31.0 + 0.5);
        
        vec3 O;
        
        if (semiTransparencyMode == 0)
            O = (Bi + Fi) * 0.5;
        else if (semiTransparencyMode == 1)
            O = Bi + Fi;
        else
            O = Bi + Fi * 0.25;
        
        O = clamp(O, 0.0, 31.0);
        
        // Convert back to display gamma
        vec3 final = pow(O / 31.0, vec3(invGamma));
        outColor = vec4(final, 1.0);
    } else {
        // No transparency
        outColor = F;
    }
    
    fragColor = vec4(outColor.rgb, 1.0);
}